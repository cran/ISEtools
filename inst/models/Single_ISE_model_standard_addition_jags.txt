model {
	### Calculate x from log x
	for (i in 1:N) {
		x[i] <- pow(10, log10x[i])
	}
	
	### Calibration data
	# Each ISE emf response is based on x and a, b, c, and tau for the particular ISE
	for (i in 1:N) {
		emf[i] ~ dnorm(mu.emf[i], Tau[i])
		mu.emf[i] <- a + b *log(x[i] + c)/log(10)
		Tau[i] <- tau
	}
	
	### Priors for the ISE model
	a ~ dnorm(0, 0.000001)
	
	###########################
	# ISE-specific priors     #
	b ~ dnorm(mu.b, 0.01)     #
	cstar ~ dunif(0.1, 0.5)   #
	sigma ~ dunif(0,10)       #
	###########################

	# Logical nodes
	c <- pow(cstar, 10)	
	logsigma <- log(sigma)
	tau <- 1/(sigma*sigma)

	###  Experimental Samples
	for (i in 1:M) {
		##################################
		# Prior on log x                 #
		log10x.exp[i] ~ dunif(-12, -2)   #
		################################## 
		x.exp[i] <- pow(10, log10x.exp[i])
	}
	
	for (i in 1:M) {	
		x.sa[i] <- (V.s[i]*x.exp[i] + V.add[i]*conc.add[i])/(V.s[i] + V.add[i])
		delta.emf[i] ~ dnorm(mu.delta.emf[i], Tau.delta.emf[i])	
		mu.delta.emf[i] <- b*log((x.sa[i] + c.sa[i])/(x.exp[i] + c))/log(10)
		c.sa[i] <- c*(V.s[i]/(V.s[i]+V.add[i]))

		# SD of an individual measurement
		Tau.exp[i] <- tau
		SD.exp[i] <- 1/sqrt(Tau.exp[i])

		# SD of difference of two measurements
		SD.delta.emf[i] <- sqrt(2)*SD.exp[i]
		Tau.delta.emf[i] <- 1/(pow(SD.delta.emf[i], 2))
			
	}
}